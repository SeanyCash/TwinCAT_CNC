<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FB_NCI" Id="{4f24b964-7729-4762-90ad-698d0a17343d}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_NCI IMPLEMENTS I_HMI; 
VAR_INPUT
	bExecute			: BOOL;
    bStop               : BOOL;
    sPrgName			: STRING(255);
	
	
END_VAR
VAR_OUTPUT
	bBusy				: BOOL;
	bError				: BOOL;
	bDone				: BOOL;
    sLoadedProgram      : STRING;
    out_stItpToPlc	    : NCTOPLC_NCICHANNEL_REF;
    bSetSpindleSpeed    : BOOL;
END_VAR
VAR_IN_OUT
	stX					: AXIS_REF;
	stY					: AXIS_REF;
	stZ					: AXIS_REF;

END_VAR
VAR
	fbSpindle           : FB_SpindleFunctions;
    bSpindleAtSpeed     : BOOL;
    bSpindleActive      : BOOL;
    nState				: UDINT	:= 0;
	nErrorState			: UDINT;
	fChannelOverride    : REAL;
	fbMessage		    : FB_TcMessage;
	fbBuildGroup		    : CfgBuildExt3DGroup;
	fbLoadGCode			    : ItpLoadProgEx;
	fbStartStopGCode	    : ItpStartStopEx;
	fbClearGroup		    : CfgReconfigGroup;
	fbConfirmMCodeHsk		: ItpConfirmHsk;
    bHskBusy                : BOOL;
    bHskError               : BOOL;
    nHskErrorID             : UDINT;
	fbZeroShiftOffset	    : ItpWriteZeroShiftEx;
    fbReadZeroShiftOffset   : ItpReadZeroShiftEx;
    bExecuteZeroShift	    : BOOL;
    bZeroShiftWriteBusy     : BOOL;
    bReadZeroShift          : BOOL;
    nZeroShiftNumber	    : UDINT := 54;
    bSpindleSpeedSet        : BOOL;
    nMCodeSeq               : INT;
    bExecuteSpindleSpeed    : BOOL;
    bExecuteSpindleEnable   : BOOL;
	
	nInterpreterState	: UDINT	:= 0;
	bConfirmHsk			: BOOL	:= FALSE;
	
	fbADSRead		: ADSREADEX;
	fbADSWrite		: ADSWRITE;

	sReadNCLine		    : STRING[255];
    bReadNCLineExecute  : BOOL;
	sReadNCName		    : STRING[100];
    bReadNCNameExec     : BOOL;
	sReadNCFileInfo	    : STRING;
	NC_In			    : ST_Interpreter_Inputs;
	NC_Out			    : ST_Interpreter_Outputs;

//=====================Interpreter Channel Info ====================

	eInterpreterState   : E_InterpreterState;
	i_stItpToPlc	AT %I*:	NcToPlc_NciChannel_Ref;
	q_stPlcToItp	AT %Q*:	PLCTONC_NCICHANNEL_REF;
	stItpToPlc			: NCTOPLC_NCICHANNEL_REF;
	stPlcToItp			: PLCTONC_NCICHANNEL_REF;
    
    fbAlarm			: FB_TcAlarm;
    bSendEvent    : BOOL;
    bInitEvent    : BOOL;
    
// M Code Triggers

rTrigM03          : R_TRIG;
rTrigM05          : R_TRIG;
tM3Pulse          : TP;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[M_InterpreterState();
M_NCName();
M_NCLine();
M_HMI_In();
M_HMI_Out();
M_StartStop();
M_SetOverride();
M_ExecuteProgram();
M_MCodeFunctions();
M_ZeroShiftOffset();
// Call Spindle control from here. Had it in CNC function block but NC controls the spindle speed and start stop so it makes sense to be here. I think.
fbSpindle(nSpindleSpeedFromNC := stItpToPlc.SpindleRpm,
          bSpindleAtRequestedSpeed => bSpindleAtSpeed,);

stItpToPlc := i_stItpToPlc;
// Pass NC status out to other FBs
out_stItpToPlc := i_stItpToPlc;
//stPlcToItp := q_stPlcToItp;
//q_stPlcToItp := stPlcToItp;

IF bExecuteZeroShift AND nZeroShiftNumber <> 0 THEN
    NC_In.ZeroShiftDesc[nZeroShiftNumber].fShiftX := stX.NcToPlc.ActPos;
    NC_In.ZeroShiftDesc[nZeroShiftNumber].fShiftY := stY.NcToPlc.ActPos;
    NC_In.ZeroShiftDesc[nZeroShiftNumber].fShiftZ := stZ.NcToPlc.ActPos;
END_IF;
]]></ST>
    </Implementation>
    <Method Name="M_ExecuteProgram" Id="{f5d89ea2-b283-4fc0-aa63-1e3309b96a56}">
      <Declaration><![CDATA[METHOD M_ExecuteProgram : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[

// handling of m-functions is done outside statemachine
// all m-functions are directly confiremd. In a real application we have of course to consider the m-function numbers to turn on/off certain functions in the machine.
	

fbConfirmMCodeHsk(
	bExecute:=bConfirmHsk , 
	sNciToPlc:=stItpToPlc , 
	sPlcToNci:=q_stPlcToItp , 
	bBusy=> bHskBusy, 
	bErr=> , 
	nErrId=> );



IF bStop THEN 
    nState :=0;
END_IF

CASE nState OF
0:
	IF bExecute THEN
		bBusy	:= TRUE;
		bDone	:= FALSE;
		bError	:= FALSE;
		nErrorState	:= nState;
        fbMessage.CreateEx(TC_Events.CNC_Events.NC_Execute_Started,0); 
        fbMessage.Send(0);
        fbClearGroup(bExecute:=FALSE); 
        // Check for Y axis geared out before. Can cause problems re-starting a g code run
        bGearOutExecute:=TRUE;
        IF fbCNC.Y_MC_OUT.bGearOutDone THEN
            bGearOutExecute:=FALSE;
		    nState	:= 5;           
        END_IF
        
	END_IF	
5:

	fbClearGroup(
		bExecute:=TRUE, 
		nGroupId:=ItpGetGroupId(sNciToPlc:=stItpToPlc) , 
		tTimeOut:= , 
		bBusy=> , 
		bErr=> , 
		nErrId=> );
	IF NOT fbClearGroup.bBusy THEN
		IF NOT fbClearGroup.bErr THEN
			nState	:= 10;
		ELSE
			(* add error handling *)
			nErrorState	:= nState;
			bBusy	:= FALSE;
			bError	:= TRUE;
			nState	:= 9999;				
		END_IF
		fbClearGroup(bExecute:=FALSE); 
	END_IF
	
10:
	// first build interpolation group 
	fbBuildGroup(
		bExecute:=TRUE, 
		nGroupId:=ItpGetGroupId(sNciToPlc:=stItpToPlc) , 
		nXAxisId:=stX.NcToPlc.AxisId, 
		nYAxisId:=stY.NcToPlc.AxisId, 
		nZAxisId:=stZ.NcToPlc.AxisId, 
		nQ1AxisId:= 0, 
		nQ2AxisId:= 0, 
		nQ3AxisId:= 0, 
		nQ4AxisId:= 0, 
		nQ5AxisId:= 0, 
		tTimeOut:= , 
		bBusy=> , 
		bErr=> , 
		nErrId=> );
	IF NOT fbBuildGroup.bBusy THEN
		IF NOT fbBuildGroup.bErr THEN
            bGearInExecute:=FALSE;
			nState	:= 15;
		ELSE
			(* add error handling *)
			nErrorState	:= nState;
			bBusy	:= FALSE;
			bError	:= TRUE;
			nState	:= 9999;
		END_IF
		fbBuildGroup(bExecute:=FALSE); 
	END_IF
    
    
15:

   IF fbCNC.Y_Ref.NcToPlc.CoupleState <> 1 THEN
    bGearInExecute:=FALSE;
    IF NOT bGearInExecute THEN
    nState:=16;
    END_IF
   ELSIF fbCNC.Y_Ref.NcToPlc.CoupleState = 1 THEN
    nState:=20;
   END_IF
   
16:
    bGearInExecute:=TRUE;
    IF fbCNC.Y_MC_OUT.bGearInDone AND (fbCNC.Y_Ref.NcToPlc.CoupleState = 1) THEN
        nState:=20;
    END_IF
     
20:
	// load g-code file 
	fbLoadGCode(
		sNciToPlc:=stItpToPlc, 
		bExecute:=TRUE, 
		sPrg:= sPrgName, 
		nLength:= INT_TO_UDINT(LEN(sPrgName)), 
		tTimeOut:= , 
		bBusy=> , 
		bErr=> , 
		nErrId=> );
	IF NOT fbLoadGCode.bBusy THEN
		IF NOT fbLoadGCode.bErr THEN
			nState	:= 30;
		ELSE
			(* add error handling *)
			nErrorState	:= nState;
			bBusy	:= FALSE;
			bError	:= TRUE;
			nState	:= 9999;				
		END_IF
		fbLoadGCode(sNciToPlc:=stItpToPlc, bExecute:=FALSE); 
	END_IF
	
30:
	// we have to check the status of the interpreter. It has to be in ready state, in case of loading the program successfully.
	nInterpreterState	:= ItpGetStateInterpreter(sNciToPlc:=stItpToPlc);
	IF nInterpreterState = Tc2_NCI.NCI_INTERPRETER_READY THEN
		nState	:= 40;
	ELSE
		(* add error handling *)
		nErrorState	:= nState;
		bBusy	:= FALSE;
		bError	:= TRUE;
		nState	:= 9999;				
	END_IF
	
40:
	// start g-code file
	fbStartStopGCode(
		bStart:=TRUE, 
		bStop:=FALSE, 
		tTimeOut:= , 
		sNciToPlc:= stItpToPlc, 
		bBusy=> , 
		bErr=> , 
		nErrId=> );
	IF NOT fbStartStopGCode.bBusy THEN
		IF NOT fbStartStopGCode.bErr THEN
			nState	:= 50;
		ELSE	
			(* add error handling *)
			nErrorState	:= nState;
			bBusy	:= FALSE;
			bError	:= TRUE;
			nState	:= 9999;				
		END_IF
		fbStartStopGCode( bStart:=FALSE, sNciToPlc:= stItpToPlc ); 
	END_IF
	
50:
	// check state, again - we are at least not in ready state for several ticks
	// this is to ensure that we don't indicate program has finished, before we have started 
	nInterpreterState	:= ItpGetStateInterpreter(sNciToPlc:=stItpToPlc);
	IF nInterpreterState <> Tc2_NCI.NCI_INTERPRETER_READY THEN
		// nci is running
		nState	:= 60;
	END_IF
     
60:
	nInterpreterState	:= ItpGetStateInterpreter(sNciToPlc:=stItpToPlc);
	IF	nInterpreterState = Tc2_NCI.NCI_INTERPRETER_READY THEN
		// program has finished
		nState	:= 65;
	ELSIF nInterpreterState = Tc2_NCI.NCI_INTERPRETER_ABORTED THEN
		nState := 0;
	END_IF

65:
    bGearInExecute:=FALSE;
    bGearOutExecute:=TRUE;
    IF fbCNC.Y_MC_OUT.bGearOutDone AND (fbCNC.Y_Ref.NcToPlc.CoupleState = 0) THEN
        bGearOutExecute:=FALSE;
        nState:=70;
    END_IF
	
70:
    
	// program has finished 
	// now clear interpolation group 
	fbClearGroup(
		bExecute:=TRUE, 
		nGroupId:=ItpGetGroupId(sNciToPlc:=stItpToPlc) , 
		tTimeOut:= , 
		bBusy=> , 
		bErr=> , 
		nErrId=> );
	IF NOT fbClearGroup.bBusy THEN
		IF NOT fbClearGroup.bErr THEN
			nState	:= 80;
		ELSE
			(* add error handling *)
			nErrorState	:= nState;
			bBusy	:= FALSE;
			bError	:= TRUE;
			nState	:= 9999;				
		END_IF
		fbClearGroup(bExecute:=FALSE); 
	END_IF
80:
	bDone	:= TRUE;
	bBusy	:= FALSE;
	IF NOT bExecute THEN
        fbMessage.CreateEx(TC_Events.CNC_Events.NC_Execute_Finished,0); 
        fbMessage.Send(0); 
		nState	:= 0;
	END_IF
9999:
	// error state 
	IF NOT bExecute THEN
        fbMessage.CreateEx(TC_Events.CNC_Events.NC_Execute_Failed,0); 
        fbMessage.Send(0);
        bGearOutExecute:=TRUE; 
	    nState	:= 0;
	    bError	:= FALSE;
	END_IF
END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_HMI_In" Id="{9f4f47f9-fd80-4bd7-98c1-19ae73c5acf0}">
      <Declaration><![CDATA[METHOD M_HMI_In : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Set NC Channel Override Speed
fChannelOverride := stHMI_In.fChannelOverride;

//stHMI_In.sPrgName;
// Spindle Override Speed
q_stPlcToItp.ChnSpindleOvr := stHMI_In.fSpindleOverrideSpeed;

// Zero Shift Offset

bExecuteZeroShift := stHMI_In.bExecuteZeroShift;
nZeroShiftNumber := stHMI_In.nZeroShiftNumber;
bReadZeroShift := stHMI_In.bReadZeroShift;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_HMI_Out" Id="{c9b11abe-3756-440a-87f2-c576c5174866}">
      <Declaration><![CDATA[METHOD M_HMI_Out : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Interpreter Channel State

stHMI_Out.sInterpreterState := TO_STRING(eInterpreterState);

// Channel Override Speed

stHMI_Out.fChannelOverrideSpeed := fChannelOverride;

IF i_stItpToPlc.PathVelo <>0 THEN
    stHMI_Out.fToolpathVelocityIPM := (i_stItpToPlc.PathVelo / 0.42);
ELSE
 stHMI_Out.fToolpathVelocityIPM := 0.0;  
END_IF

// Interpreter Status to HMI
stHMI_Out.stItpToPlc := i_stItpToPlc;

stHMI_Out.ZeroShiftDesc[54]:= NC_In.ZeroShiftDesc[54];
stHMI_Out.ZeroShiftDesc[55]:= NC_In.ZeroShiftDesc[55];
stHMI_Out.ZeroShiftDesc[56]:= NC_In.ZeroShiftDesc[56];
stHMI_Out.ZeroShiftDesc[57]:= NC_In.ZeroShiftDesc[57];
// NC program info
stHMI_Out.sReadNCLine := sReadNCLine;
stHMI_Out.sReadNCName := sReadNCName;

// Get Current M Code
stHMI_Out.nCurrentMCode:= ItpGetHskMFunc(sNciToPlc:=stItpToPlc);]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_InterpreterState" Id="{20895469-b475-4424-b32a-e6207df46880}">
      <Declaration><![CDATA[METHOD M_InterpreterState : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Interpreter State mapped from fbCNC call
eInterpreterState := i_stItpToPlc.ItpState;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_MCodeFunctions" Id="{9076bbcc-6dde-4307-b020-d9bfdd475baf}">
      <Declaration><![CDATA[METHOD M_MCodeFunctions : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Get Current M Code
nMCodeSeq:= ItpGetHskMFunc(sNciToPlc:=stItpToPlc);

CASE nMCodeSeq OF 

0:

    bConfirmHsk	:= FALSE;
    bExecuteSpindleSpeed := FALSE;
    bExecuteSpindleEnable := FALSE;
    bSpindleActive := FALSE;
    tM3Pulse (IN:=FALSE);

3:
  
    IF NOT bConfirmHsk THEN 
     tM3Pulse (IN:=TRUE,PT:=T#500MS);
     bSpindleActive   := fbSpindle.M_SetSpeed(bExecute:=tM3Pulse.Q);
    END_IF
    IF bSpindleActive THEN
      //tM3Pulse (IN:=TRUE,PT:=T#500MS);
      bConfirmHsk	:= TRUE;
    ELSE	
      bConfirmHsk	:= FALSE;
    END_IF

END_CASE



]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_NCFileInfo" Id="{b55ec5ca-99a1-4a79-94b9-b4f299483b66}">
      <Declaration><![CDATA[METHOD M_NCFileInfo : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbADSRead(
					NETID:=,
					PORT:=500,
					IDXGRP:=8449,
					IDXOFFS:=7,
					LEN:=,
					DESTADDR:=,
					READ:=,
					TMOUT:=,
					BUSY=>,
					ERR=>,
					ERRID=>
		 						);						
								]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_NCLine" Id="{7b737864-7f89-45b9-8635-b6cfe3b92a12}">
      <Declaration><![CDATA[METHOD M_NCLine : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbADSRead(
					NETID:=,
					PORT:=500,
					IDXGRP:=16#2302,
					IDXOFFS:=16#20000001,
					LEN:=SIZEOF(sReadNCLine),
					DESTADDR:=ADR(sReadNCLine),
					READ:=bReadNCLineExecute,
					TMOUT:=,
					BUSY=>,
					ERR=>,
					ERRID=>
		 						);]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_NCName" Id="{2579f1bc-bd44-45c1-9788-bb6e35380df4}">
      <Declaration><![CDATA[METHOD M_NCName : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[sLoadedProgram:=sPrgName;

fbADSRead(
					NETID:=,
					PORT:=500,
					IDXGRP:=16#2102,
					IDXOFFS:=16#7,
					LEN:=SIZEOF(sReadNCName),
					DESTADDR:=ADR(sReadNCName),
					READ:=TRUE,
					TMOUT:=,
					BUSY=>,
					ERR=>,
					ERRID=>
		 						);]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetOverride" Id="{6d44a2c7-8e8d-49f2-9eee-f598e4c03a01}">
      <Declaration><![CDATA[METHOD M_SetOverride : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ItpSetOverridePercent(fOverridePercent:=fChannelOverride , sPlcToNci:=q_stPlcToItp);]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StartStop" Id="{b54daf30-43fd-4356-b2ab-51de72eb9059}">
      <Declaration><![CDATA[METHOD M_StartStop : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbStartStopGCode( bStop:=bStop, sNciToPlc:= stItpToPlc );]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_ZeroShiftOffset" Id="{2a915930-d001-41c5-9e1b-3c451820ac54}">
      <Declaration><![CDATA[METHOD M_ZeroShiftOffset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nZeroShiftNumber <> 0 THEN

fbZeroShiftOffset(
	bExecute:= bExecuteZeroShift, 
	nZsNo:= nZeroShiftNumber, 
	tTimeOut:= , 
	sNciToPlc:= stItpToPlc, 
	sZeroShiftDesc:= NC_In.ZeroShiftDesc[nZeroShiftNumber], 
	bBusy=> bZeroShiftWriteBusy, 
	bErr=> , 
	nErrId=> );
	
(*fbReadZeroShiftOffset(
	bExecute:= NOT bZeroShiftWriteBusy, 
	nZsNo:= nZeroShiftNumber, 
	tTimeOut:= , 
	sNciToPlc:= stItpToPlc, 
	sZeroShiftDesc:= NC_In.ZeroShiftDesc[nZeroShiftNumber], 
	bBusy=> , 
	bErr=> , 
	nErrId=> );	*)
    
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_NCI">
      <LineId Id="520" Count="1" />
      <LineId Id="773" Count="0" />
      <LineId Id="522" Count="0" />
      <LineId Id="518" Count="0" />
      <LineId Id="524" Count="0" />
      <LineId Id="574" Count="0" />
      <LineId Id="630" Count="0" />
      <LineId Id="997" Count="0" />
      <LineId Id="843" Count="0" />
      <LineId Id="851" Count="0" />
      <LineId Id="847" Count="0" />
      <LineId Id="846" Count="0" />
      <LineId Id="634" Count="0" />
      <LineId Id="633" Count="0" />
      <LineId Id="708" Count="0" />
      <LineId Id="707" Count="0" />
      <LineId Id="635" Count="0" />
      <LineId Id="1080" Count="0" />
      <LineId Id="639" Count="0" />
      <LineId Id="207" Count="0" />
      <LineId Id="291" Count="0" />
      <LineId Id="636" Count="1" />
      <LineId Id="641" Count="0" />
      <LineId Id="290" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_ExecuteProgram">
      <LineId Id="320" Count="0" />
      <LineId Id="324" Count="10" />
      <LineId Id="321" Count="2" />
      <LineId Id="8" Count="11" />
      <LineId Id="253" Count="2" />
      <LineId Id="258" Count="2" />
      <LineId Id="263" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="264" Count="0" />
      <LineId Id="241" Count="0" />
      <LineId Id="21" Count="58" />
      <LineId Id="248" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="249" Count="0" />
      <LineId Id="81" Count="11" />
      <LineId Id="94" Count="65" />
      <LineId Id="283" Count="0" />
      <LineId Id="161" Count="43" />
      <LineId Id="243" Count="1" />
      <LineId Id="205" Count="4" />
      <LineId Id="236" Count="0" />
      <LineId Id="240" Count="0" />
      <LineId Id="257" Count="0" />
      <LineId Id="210" Count="3" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_HMI_In">
      <LineId Id="5" Count="1" />
      <LineId Id="18" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="24" Count="1" />
    </LineIds>
    <LineIds Name="FB_NCI.M_HMI_Out">
      <LineId Id="5" Count="5" />
      <LineId Id="4" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="21" Count="2" />
      <LineId Id="30" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="34" Count="2" />
      <LineId Id="39" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="43" Count="1" />
      <LineId Id="42" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_InterpreterState">
      <LineId Id="7" Count="0" />
      <LineId Id="6" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_MCodeFunctions">
      <LineId Id="66" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="105" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="107" Count="0" />
      <LineId Id="106" Count="0" />
      <LineId Id="112" Count="1" />
      <LineId Id="122" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="111" Count="0" />
      <LineId Id="123" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="100" Count="0" />
      <LineId Id="121" Count="0" />
      <LineId Id="101" Count="2" />
      <LineId Id="99" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="47" Count="0" />
      <LineId Id="54" Count="1" />
      <LineId Id="46" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_NCFileInfo">
      <LineId Id="5" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="6" Count="10" />
      <LineId Id="24" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_NCLine">
      <LineId Id="6" Count="11" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_NCName">
      <LineId Id="21" Count="1" />
      <LineId Id="6" Count="11" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_SetOverride">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_StartStop">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_NCI.M_ZeroShiftOffset">
      <LineId Id="31" Count="1" />
      <LineId Id="6" Count="7" />
      <LineId Id="5" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="19" Count="7" />
      <LineId Id="15" Count="0" />
      <LineId Id="33" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>